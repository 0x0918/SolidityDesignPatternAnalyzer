\appendix
\chapter{Appendice}
\setcounter{secnumdepth}{0}
{\section{Codice di riferimento degli Authorization Design Pattern}\label{appendix:codici}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Access Restriction}, label={appendix:access_restriction}]
contract AccessRestriction {
	uint256 public creationTime = block.timestamp;
	modifier onlyBefore(uint256 _time) {
		require(block.timestamp < _time);
		_;
	}
	modifier minAmount(uint256 _amount) {
		require(msg.value >= _amount);
		_;
	}
	function test() public payable onlyBefore(creationTime + 10 seconds) minAmount(1 ether) {
		// Do something
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Ownership}, label={appendix:ownership}]
contract Ownable {
	address private _owner;
	event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
	constructor() {
		_owner = msg.sender;
		emit OwnershipTransferred(address(0), _owner);
	}
	modifier onlyOwner() {
		require(_owner == msg.sender, "Ownable: caller is not the owner");
		_;
	}
}
\end{lstlisting}}
}
\newpage
{\section{Codice di riferimento dei Behavioral Design Pattern}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Commit and Reveal}, label={appendix:commit_and_reveal}]
contract CommitReveal {
	struct Commit {string choice; string secret; string status;}
	mapping(address => mapping(bytes32 => Commit)) public userCommits;
	event LogCommit(bytes32, address);
	event LogReveal(bytes32, address, string, string);
	function commit(bytes32 _commit) public returns (bool success) {
		Commit storage userCommit = userCommits[msg.sender][_commit];
		if (bytes(userCommit.status).length != 0)
			return false;
		userCommit.status = "c";
		emit LogCommit(_commit, msg.sender);
		return true;
	}
	function reveal(string calldata _choice, string calldata _secret, bytes32 _commit) public returns (bool success) {
		Commit storage userCommit = userCommits[msg.sender][_commit];
		bytes memory bytesStatus = bytes(userCommit.status);
		if (bytesStatus.length == 0)
			return false;
		else if (bytesStatus[0] == "r")
			return false;
		if (_commit != keccak256(abi.encodePacked(_choice, _secret)))
			return false; 
		userCommit.choice = _choice;
		userCommit.secret = _secret;
		userCommit.status = "r";
		emit LogReveal(_commit, msg.sender, _choice, _secret);
		return true;
	}
	function traceCommit(address _address, bytes32 _commit) public view
	returns (string memory choice, string memory secret, string memory status) {
		Commit storage userCommit = userCommits[_address][_commit];
		require(bytes(userCommit.status)[0] == "r");
		return (userCommit.choice, userCommit.secret, userCommit.status);
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per GuardCheck}, label={appendix:guardcheck}]
contract GuardCheck {
	function donate(address addr) payable public {
		require(addr != address(0));
		require(msg.value != 0);
		uint balanceBeforeTransfer = address(this).balance;
		uint transferAmount;
		if (addr.balance == 0) {
			transferAmount = msg.value;
		} else if (addr.balance < msg.sender.balance) {
			transferAmount = msg.value / 2;
		} else {
			revert();
		}
		payable(addr).transfer(transferAmount);
		assert(address(this).balance == balanceBeforeTransfer - transferAmount);      
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Oracle}, label={appendix:oracle}]
contract Oracle {
	address knownSource = address(0x1234);
	struct Request {
		bytes data;
		function(bytes memory) external callback;
	}
	Request[] requests;
	event NewRequest(uint256);
	modifier onlyBy(address account) {
		require(msg.sender == account);
		_;
	}
	function query(bytes memory data, function(bytes memory) external callback) public
	{
		requests.push(Request(data, callback));
		emit NewRequest(requests.length - 1);
	}
	function reply(uint256 requestID, bytes memory response) public onlyBy(knownSource)
	{
		requests[requestID].callback(response);
	}
}
contract OracleConsumer {
	Oracle oracle = Oracle(address(0x4321));
	modifier onlyBy(address account) { 
		require(msg.sender == account);  _; 
	}
	function updateExchangeRate() public {
		oracle.query("USD", this.oracleResponse);
	}
	function oracleResponse(bytes memory response) public onlyBy(address(oracle)) {
		// do something with response
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Pull Over Push}, label={appendix:pull_over_push}]
contract Auction {
	address public highestBidder;
	uint highestBid;
	mapping(address => uint) refunds;
	function bid() public payable {
		require(msg.value >= highestBid);
		if (highestBidder != address(0)) {
			refunds[highestBidder] += highestBid; 
		}
		highestBidder = msg.sender;
		highestBid = msg.value;
	}
	function withdrawRefund() public {
		uint refund = refunds[msg.sender];
		refunds[msg.sender] = 0;
		payable(msg.sender).transfer(refund);
	}
}
\end{lstlisting}}

\newpage

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Randomness}, label={appendix:randomness}]
contract SimpleRandom {
	function simpleRandomNumber() internal view returns (uint) {
		return uint(blockhash(block.number - 1));
	}
	function seededRandomNumber(string calldata seed) internal view returns (uint) {
		return uint(keccak256(abi.encodePacked(blockhash(block.number-1), seed)));
	}
	function random() public view returns (uint) {
		return uint(keccak256(abi.encodePacked(block.timestamp, msg.sender, block.difficulty)));
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per State Machine}, label={appendix:state_machine}]
contract DepositLock {
	enum Stages {
		AcceptingDeposits,
		FreezingDeposits,
		ReleasingDeposits
	}
	Stages public stage = Stages.AcceptingDeposits;
	uint256 public creationTime = block.timestamp;
	mapping(address => uint256) balances;
	
	modifier atStage(Stages _stage) {
		require(stage == _stage);
		_;
	}
	modifier timedTransitions() {
		if (stage == Stages.AcceptingDeposits && block.timestamp >= creationTime + 1 days) nextStage();
		if (stage == Stages.FreezingDeposits && block.timestamp >= creationTime + 8 days) nextStage();
		_;
	}
	function nextStage() internal {
		stage = Stages(uint256(stage) + 1);
	}
	function deposit() public payable timedTransitions atStage(Stages.AcceptingDeposits) {
		balances[msg.sender] += msg.value;
	}
	
	function withdraw() public timedTransitions atStage(Stages.ReleasingDeposits) {
		uint256 amount = balances[msg.sender];
		balances[msg.sender] = 0;
		payable(msg.sender).transfer(amount);
	}
}
\end{lstlisting}}}
\newpage
{\section{Codice di riferimento dei Gas Economic Design Pattern}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Memory Array Building}, label={appendix:memory_array_building}]
contract MemoryArrayBuilding {
	struct Item {
		string name;
		string category;
		address owner;
		uint32 zipcode;
		uint32 price;
	}
	Item[] public items;
	mapping(address => uint) public ownerItemCount;
	function getItemIDsByOwner(address _owner) public view returns (uint[] memory) {
		uint[] memory result = new uint[](ownerItemCount[_owner]);
		uint counter = 0;
		for (uint i = 0; i < items.length; i++) {
			if (items[i].owner == _owner) {
				result[counter] = i;
				counter++;
			}
		}
		return result;
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per String Equality Comparison}, label={appendix:string_equality_comparison}]
contract SimpleContractExample {
	function hashCompareCheck(string memory _a, string memory _b) internal pure returns (bool) {
		return
		keccak256(abi.encodePacked(_a)) == keccak256(abi.encodePacked(_b));
	}
	function hashCompareWithLengthCheck(string memory _a, string memory _b) internal pure returns (bool) {
		if (bytes(_a).length != bytes(_b).length) {
			return false;
		} else {
			return keccak256(abi.encodePacked(_a)) == keccak256(abi.encodePacked(_b));
		}
	}
}
\end{lstlisting}}
\newpage
{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Tight Variable Packing}, label={appendix:tight_variable_packing}]
contract StructPackingExample {
	struct CheapStruct {
		uint8 a;
		uint8 b;
		uint8 c;
		uint8 d;
		bytes1 e;
		bytes1 f;
		bytes1 g;
		bytes1 h;
	}
	CheapStruct example;
	function addCheapStruct() public {
		CheapStruct memory someStruct = CheapStruct(1,2,3,4,"a","b","c","d");
		example = someStruct;
	}
}
\end{lstlisting}}}
{\section{Codice di riferimento dei Lifecycle Design Pattern}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Auto Deprecation}, label={appendix:auto_deprecation}]
contract Deprecatable {
	uint expires;
	constructor(uint _lifetime) { 
		expires = block.timestamp + _lifetime;
	}
	function expired() public view returns (bool) { 
		return block.timestamp > expires ? true : false;
	}
	modifier willDeprecate() { 
		if (!expired()) {_;}
	}
	modifier whenDeprecated() { 
		if (expired()) {_;}
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Mortal}, label={appendix:mortal}]
contract Mortal is Ownable {
	function destroy() public onlyOwner { 
		selfdestruct(payable(_owner)); 
	}
	function destroyWithoutModifier() public {
		if (msg.sender == owner())
		selfdestruct(payable(_owner)); 
	}
}
\end{lstlisting}}}
\newpage
{\section{Codice di riferimento dei Maintenance Design Pattern}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Data Segregation}, label={appendix:data_segregation}]
contract Storage {
	mapping(bytes32 => uint256) public uintStorage;
	mapping(bytes32 => address) public addressStorage;
	function setUintStorage(bytes32 key, uint256 value) public {
		uintStorage[key] = value;
	}
	function setaddressStorage(bytes32 key, address value) public {
		addressStorage[key] = value;
	}
}
contract Logic {
	Storage _storage;
	constructor(address storageAddress) {
		_storage = Storage(storageAddress);
	}
	function test() public returns (uint256) {
		bytes32 key = keccak256("capybara");
		_storage.setUintStorage(key, 911);
		return _storage.uintStorage(key);
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Register}, label={appendix:register}]
contract Register is Ownable {
	address public currentVersion;
	address[] previousVersions;
	function updateContract(address newVersion) public onlyOwner returns (bool) {
		if (newVersion != currentVersion) {
			previousVersions.push(currentVersion);
			currentVersion = newVersion;
			return true;
		}
		return false;
	}
	fallback() external {
		(bool success, ) = currentVersion.delegatecall(msg.data);
		require(success);
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Relay}, label={appendix:relay}]
contract Relay is Ownable {
	address public currentVersion;
	constructor(address initAddr) { currentVersion = initAddr; }
	function updateContract(address newVersion) public onlyOwner {
		currentVersion = newVersion;
	}
	fallback() external {
		(bool success, ) = currentVersion.delegatecall(msg.data);
		require(success);
	}
}
\end{lstlisting}}
}
{\section{Codice di riferimento dei Security Design Pattern}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Balance Limit}, label={appendix:balance_limit}]
contract BalanceLimit {
	uint256 limit;
	constructor(uint256 _value) {
		limit = _value;
	}
	modifier limitedPayable() {
		require(address(this).balance <= limit);
		_;
	}
	function deposit() public payable limitedPayable {
		// TO-DO Something
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Check Effects Interactions}, label={appendix:check_effects_interactions}]
contract ChecksEffectsInteractions {
	mapping(address => uint256) balances;
	function deposit() public payable {
		balances[msg.sender] = msg.value;
	}
	function withdraw(uint256 amount) public {
		require(balances[msg.sender] >= amount);
		balances[msg.sender] -= amount;
		payable(msg.sender).transfer(amount);
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Emergency Stop}, label={appendix:emergency_stop}]
contract EmergencyStop is Ownable {
	bool private contractStopped = false;
	modifier haltInEmergency() {
		if (!contractStopped) _;
	}
	modifier enableInEmergency() {
		if (contractStopped) _;
	}
	function toggleContractStopped() public onlyOwner {
		contractStopped = !contractStopped;
	}
	function deposit() public payable haltInEmergency {
		// some code
	}
	function withdraw() public view enableInEmergency {
		// some code
	}
}
\end{lstlisting}}
\newpage
{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Mutex}, label={appendix:mutex}]
contract Mutex {
	bool locked = false;
	modifier noReentrancy() {
		require(!locked);
		locked = true;
		_;
		locked = false;
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Rate Limit}, label={appendix:rate_limit}]
contract RateLimiter {
	uint256 executions;
	uint256 enableEvery;
	uint256 nextReset;
	constructor(uint256 _resetInterval) {
		executions = 0;
		enableEvery = _resetInterval;
		nextReset = block.timestamp + _resetInterval;
	}
	function reset() private {
		executions = 0;
		nextReset = block.timestamp + enableEvery;
	}
	modifier rateLimited(uint256 maxExecutions) {
		if (executions++ < maxExecutions) _;
		if (block.timestamp >= nextReset) reset();
	}
}
\end{lstlisting}}

{\begin{lstlisting}[language=Solidity, caption={Codice di riferimento per Rejector}, label={appendix:rejector}]
contract Rejector {
	fallback() external {
		revert();
	}
}
\end{lstlisting}}
}
\newpage
\section{JSONSchema dei Design Pattern Descriptor}
{\begin{lstlisting}[language=json, caption={JSONSchema dei Design Pattern Descriptor}, label={appendix:json_schema}]
{"$schema":"http://json-schema.org/draft-07/schema#","type":"object","properties":{"name":{"type":"string"},"checks":{"type":"array","items":{"anyOf":[{"type":"object","properties":{"check_type":{"type":"string","const":"comparison"},"binary_operations":{"type":"array","items":[{"type":"object","properties":{"operator":{"type":"string"},"operand_1":{"type":"string"},"operand_2":{"type":"string"}},"required":["operator","operand_1","operand_2"]}],"uniqueItems":true,"minItems":1}},"required":["check_type","binary_operations"]},{"type":"object","properties":{"check_type":{"type":"string","const":"inheritance"},"parent_names":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","parent_names"]},{"type":"object","properties":{"check_type":{"type":"string","const":"modifier"},"modifiers":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","modifiers"]},{"type":"object","properties":{"check_type":{"type":"string","const":"rejector"}},"required":["check_type"]},{"type":"object","properties":{"check_type":{"type":"string","const":"tight_variable_packing"}},"required":["check_type"]},{"type":"object","properties":{"check_type":{"type":"string","const":"fn_return_parameters"},"parameters_list":{"type":"array","items":[{"type":"object","properties":{"storage_location":{"type":"string"},"type":{"type":"string"}},"required":["storage_location","type"]}],"uniqueItems":true,"minItems":1}},"required":["check_type","parameters_list"]},{"type":"object","properties":{"check_type":{"type":"string","const":"memory_array_building"}},"required":["check_type"]},{"type":"object","properties":{"check_type":{"type":"string","const":"fn_call"},"callable_function":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","callable_function"]},{"type":"object","properties":{"check_type":{"type":"string","const":"fn_definition"},"fn_names":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","fn_names"]},{"type":"object","properties":{"check_type":{"type":"string","const":"event_emit"},"event_names":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","event_names"]},{"type":"object","properties":{"check_type":{"type":"string","const":"enum_definition"},"enum_names":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","enum_names"]},{"type":"object","properties":{"check_type":{"type":"string","const":"check_effects_interaction"}},"required":["check_type"]},{"type":"object","properties":{"check_type":{"type":"string","const":"state_toggle"},"state_names":{"type":"array","items":[{"type":"string"}],"uniqueItems":true,"minItems":1}},"required":["check_type","state_names"]},{"type":"object","properties":{"check_type":{"type":"string","const":"relay"}},"required":["check_type"]},{"type":"object","properties":{"check_type":{"type":"string","const":"eternal_storage"}},"required":["check_type"]}]},"uniqueItems":true,"minItems":1}},"required":["name","checks"]}\end{lstlisting}}